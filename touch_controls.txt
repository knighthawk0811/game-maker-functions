//************************************************************//
//GLOBAL VARIABLES
//main camera
global._camera_moving = false;
global._camera = view_get_camera(0); // get camera object
if(view_visible[0])
{
	//global._camera_x = camera_get_view_x(global._camera);
	//global._camera_y = camera_get_view_y(global._camera);
	global._camera_width = camera_get_view_width(global._camera);
	global._camera_height = camera_get_view_height(global._camera);
}

//************************************************************//
//PARENT OBJECT
//CREATE EVENT
//common variables
touch_id = -1;
touch_x = x;
touch_y = y;
touch_pressed = false;
press_x = x;
press_y = y;

//template functions
//ALL ui elements need to make these functions work

//actively get input
//called only by the activator
function input(_touch_id, _touch_x, _touch_y, _pressed = false)
{
    touch_id = _touch_id;
	touch_x = _touch_x;
	touch_y = _touch_y; 
	touch_pressed = _pressed;
	if(_pressed)
	{
		press_x = _touch_x;
		press_y = _touch_y;
	}
}
//no longer getting input
//called on the end step
function release()
{	
    touch_id = -1;
    touch_x = x;
    touch_y = y;   
	touch_pressed = false; 
	press_x = x;
	press_y = y;    
}
//is this UI button being held or pressed?
function is_held()
{
	if(touch_id == -1)
	{
		return false;	
	}
	return true;
}
//was this UI button pressed on this frame?
function is_pressed()
{
	if(touch_pressed)
	{
		return true;	
	}
	return false;
}
//was this touch released?
function is_released()
{
	if(device_mouse_check_button_released(touch_id, mb_left))
	{
		release();
		return true;	
	}
	return false;
}

//END STEP EVENT
is_released();

//************************************************************//
//ACTIVATOR OBJECT (NOT A CHILD)
//BEGIN STEP
//how many touch points?
var _max_devices = 4;

//check all 4 touches
for (var i = 0; i < _max_devices; i++)
{
	//touch location on screen
    var _touch_x = device_mouse_x(i);
    var _touch_y = device_mouse_y(i);
        
	//is there a ui element at this location?
    var _ui_at_pos = instance_position(_touch_x, _touch_y, obj_ui_touch_parent);
	//is the touch actually happening or is it just the mouse is sitting there not being pressed?
    var _held = device_mouse_check_button(i, mb_left);
    var _pressed = device_mouse_check_button_pressed(i, mb_left);
    
	//if the user is actually touching a ui button
    if (_held && _ui_at_pos != noone)
    {
		//call the input funciton for that ui element
		_ui_at_pos.input(i, _touch_x, _touch_y, _pressed);
    }
}

//************************************************************//
//UI BUTTON OBJECT
//CHILD of the parent
//END STEP EVENT
event_inherited();
//set your x,y where you wan tthis button on screen
x = camera_get_view_x(global._camera) + global._camera_width - 220;
y = global._camera_height - 110;

//************************************************************//
//THUMBSTICK OBJECT
//CREATE EVENT
/// @description SETUP
event_inherited();
event_inherited();


//get movement direction
function get_direction()
{
	return point_direction(press_x, press_y, touch_x, touch_y);
}
//get a value for how far the thumbstick has been pushed
// from 0 - 1
function get_speed_factor()
{	
	return clamp(point_distance(press_x, press_y, touch_x, touch_y) / (sprite_width / 2), 0, 1);	
}

//BEGIN STEP
//we need the current location for as long as this touch is held
//no matter if the touch leaves the thumbstick sprite
if (is_held())
{		
	touch_x = device_mouse_x(touch_id);
    touch_y = device_mouse_y(touch_id);
}

//DRAW
event_inherited();

//get x,y of camera
x = camera_get_view_x(global._camera) + 150;
y = camera_get_view_y(global._camera) + global._camera_height - 150;

//draw thumbstick pad
draw_sprite_ext(sprite_index,0, x, y,image_xscale,image_yscale,image_angle,image_blend,image_alpha);

//if it is being held down, we need to draw that
if(is_held())
{
	//we can't draw exactly on the touch location because the player might not be fully centered
	//and they might have dragged outside of the thumbstick pad
	var _draw_distance = clamp(point_distance(press_x, press_y, touch_x, touch_y),0,(sprite_width / 2) - 20);
	var _dir_current = get_direction();
	var _thumb_x = lengthdir_x(_draw_distance, _dir_current);
	var _thumb_y = lengthdir_y(_draw_distance, _dir_current);
	
	draw_sprite_ext(sprite_index,1, x + _thumb_x, y + _thumb_y,image_xscale,image_yscale,image_angle,image_blend,image_alpha);
	
	//for testing
	//draw_line(press_x,press_y,touch_x,touch_y);
	//draw_line(x,y,touch_x,touch_y);
}


